// run `./gradlew publishToMavenLocal` to install artifact to your Local Maven Repository

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = 'org.jspecify'
                artifactId = 'jspecify'
                name = 'JSpecify annotations'
                description = 'An artifact of well-named and well-specified annotations to power static analysis checks'
                url = 'http://jspecify.org/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:jspecify/jspecify.git'
                    developerConnection = 'scm:git:git@github.com:jspecify/jspecify.git'
                    url = 'https://github.com/jspecify/jspecify/'
                }
                developers {
                    // These are here only because Sonatype requires us to list someone.
                    // Any project member is welcome to add themselves if they want to.
                    developer {
                        id = 'kevinb9n'
                        name = 'Kevin Bourrillion'
                        email = 'kevinb@google.com'
                    }
                }
            }
        }
        conformanceTests(MavenPublication) {
            pom {
                groupId = 'org.jspecify.conformance'
                artifactId = 'conformance-tests'
                name = 'JSpecify conformance tests'
                description = 'Assertions and depdendencies representing a set of conformance tests for JSpecify'
                url = 'http://jspecify.org/'
                artifact('conformance-tests/build/distributions/conformance-tests-0.0.0-SNAPSHOT.zip').builtBy(tasks.getByPath(':conformance-tests:conformanceTestsDistZip'))
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:jspecify/jspecify.git'
                    developerConnection = 'scm:git:git@github.com:jspecify/jspecify.git'
                    url = 'https://github.com/jspecify/jspecify/'
                }
                developers {
                    developer {
                        id = 'netdpb'
                        name = 'David P. Baker'
                        email = 'dpb@google.com'
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            // For users registered in Sonatype after 24 Feb 2021
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
        }
    }
}

def privateKey = findProperty('privateKey')
def pgpPassword = findProperty('pgpPassword')

signing {
    useInMemoryPgpKeys(privateKey, pgpPassword)
    sign publishing.publications.mavenJava
}

tasks.withType(Sign).configureEach {
    onlyIf {
        privateKey?.trim() && pgpPassword?.trim()
    }
}
