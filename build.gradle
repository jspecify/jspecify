plugins {
    id 'java-library'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
    id 'com.diffplug.spotless' version '5.14.3'
    id 'net.ltgt.errorprone' version '2.0.2'
    id 'org.javamodularity.moduleplugin' version '1.8.7'
}

ext {
    junitVersion = '5.7.2'
    errorproneVersion = '2.7.1'
}

repositories { mavenCentral() }

apply from: "gradle/mrjar.gradle"
apply from: "gradle/integration-test.gradle"
apply from: "gradle/format.gradle"
apply from: "gradle/publish.gradle"

dependencies {
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
}

java {
    withJavadocJar()
    withSourcesJar()

    tasks.named('sourcesJar') {
        exclude('**.class')

        // Use the copy of NullMarked whose @Target includes MODULE.
        // TODO(cpovirk): Move configuration to mrjar.gradle? Maybe we could achieve it with task.named('sourcesJar') if mrjar.gradle were applied after we create this task?
        from sourceSets.java9.allJava
        dependsOn tasks.named('compileModuleInfoJava')
    }

    compileJava {
        // Use the copy of NullMarked whose @Target omits MODULE.
        source += fileTree(dir: 'src/main/java', include: '**/NullMarked.java')
    }
}

defaultTasks 'spotlessApply', 'build'
