/*
 * Copyright 2020 The JSpecify Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'com.diffplug.spotless' version '7.2.1'
    id 'com.github.node-gradle.node' version '7.1.0'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0' apply false
    id 'biz.aQute.bnd.builder' version '7.1.0'
}

ext {
    junitVersion = '5.13.4'
}

repositories { mavenCentral() }

def javaVersion = JavaLanguageVersion.of(System.env.JAVA_VERSION ?: 11)
java.toolchain.languageVersion = javaVersion

apply from: "gradle/mrjar.gradle"
apply from: "gradle/integration-test.gradle"
apply from: "gradle/format.gradle"
apply from: "gradle/publish.gradle"

java {
    if (javaVersion.canCompileOrRun(15)) {
        withJavadocJar()
    }
    withSourcesJar()

    sourceCompatibility = 8
    targetCompatibility = 8
    // but compile against newer JDK libraries so that we can use @Target({..., MODULE})

    tasks.named('sourcesJar') {
        exclude('**.class')
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    filePermissions {
        unix('rw-r--r--')
    }
    dirPermissions {
        unix('rwxr-xr-x')
    }
}


jar {
    manifest {
        attributes(
                "Bundle-Version": version,
                "Implementation-Version": version
                )
    }
    // Work around http://github.com/bndtools/bnd/issues/6346
    bundle {
        properties.put("project.group", provider({project.group}))
    }
}

javadoc {
    options.encoding = 'UTF-8'
}

node {
    download = true
    nodeProjectDir = file("${project.projectDir}/docs")
}

tasks.register('buildDocs') {
    group = 'Build'
    description = 'Builds the jspecify.org website.'
    dependsOn('npm_run_build')
    dependsOn('javadoc')
}

tasks.register('copyDocs', Copy) {
    dependsOn('buildDocs')
    from tasks.named('javadoc')
    into "${project.projectDir}/docs/build/docs/api"
}

tasks.named('buildDocs').get().finalizedBy('copyDocs')

defaultTasks 'spotlessApply', 'build'
